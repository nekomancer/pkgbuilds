# Maintainer: fsckd <fsckdaemon at gmail dot com>
# Contributor: veox <veox at wre dot ath dot cx>

pkgname=zsh-history-substring-search-git
pkgver=20110926
pkgrel=1
pkgdesc="A ZSH plugin to search history, a clean-room implementation of the Fish shell feature"
arch=('any')
url="https://github.com/zsh-users/zsh-history-substring-search"
license=('BSD')
depends=('zsh')
makedepends=('git')
provides=('zsh-history-substring-search')
conflicts=('zsh-history-substring-search')
install=zsh-history-substring-search-git.install
source=()
md5sums=()

_gitroot=git://github.com/zsh-users/zsh-history-substring-search.git
_gitname=zsh-history-substring-search

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
}

package() {
  cd "$srcdir/$_gitname-build"

  install -d -m755 "$pkgdir/usr/share/zsh/plugins/$_gitname"
  for f in \
    README.md \
    zsh-history-substring-search.plugin.zsh \
    zsh-history-substring-search.zsh
  do
    install -m644 $f "$pkgdir/usr/share/zsh/plugins/$_gitname/$f"
  done
}

# vim:set ts=2 sw=2 et:
